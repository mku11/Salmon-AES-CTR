plugins {
    id 'com.android.library'
    id 'maven-publish'
}

group 'com.mku.salmon'
description = 'Salmon AES256 CTR android native encryption library'
version '2.1.0'

repositories {
    maven {
        // uncomment for local repo
        allowInsecureProtocol true
        url 'http://localhost/repository/maven/releases'

        // official salmon repo
        // url 'https://github.com/mku11/Repo/raw/main/maven/releases'
    }
    mavenCentral()
}

android {
    compileSdk 34
    ndkVersion "26.1.10909125"
    defaultConfig {
        minSdk 23
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
            ndkBuild {
                 arguments "APP_OPTIM=release", "NDK_DEBUG=0", "V=1"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
            }
        }
        debug {
            debuggable true
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "../../../src/android/make/CMakeLists.txt"
            version "3.22.1"
        }
    }
    sourceSets {
        main.java.srcDirs += "../../../src/android/src/salmon-android-native"
        // workaround: since we use external source directories the IDE
        // cannot see the c source so we add them here as java files
        main.java.srcDirs += '../../../src/c'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    implementation 'com.mku.salmon:salmon-core:2.1.0'
}

publishing {
    publications {
        salmon(MavenPublication) {
            groupId 'com.mku.salmon'
            artifactId 'salmon-native-android'
            artifact("$buildDir/outputs/aar/salmon-native-android-release.aar")
            pom {
                name = project.name
                packaging = 'aar'
                description = project.description

                url = 'https://github.com/mku11/Salmon-AES-CTR'

                scm {
                    url = 'https://github.com/mku11/Salmon-AES-CTR'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/mku11/Salmon-AES-CTR/blob/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'mku'
                        name = 'Max Kas'
                    }
                }
            }
        }
    }
    repositories {
        maven  {
            url uri("${projectDir}/../../../../output/maven/releases")
        }
    }
}

publish.dependsOn 'assembleRelease'