plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'com.mku.salmon'
description = 'Salmon AES256 CTR encryption library for windows'
version '3.0.2'

import org.gradle.internal.os.OperatingSystem
OperatingSystem os = OperatingSystem.current();

println "OS: ${os.familyName}"
println "ARCH: ${System.getProperty("os.arch")}"

if (os.isLinux()) {
    project.ext.set('os', 'linux')
} else if (os.isWindows()) {
    project.ext.set('os', 'win')
} else if (os.isMacOsX()) {
    project.ext.set('os', 'macos')
}

if (System.getProperty("os.arch").equals("amd64") || System.getProperty("os.arch").equals("x86_64")) {
    project.ext.set('arch', 'x86_64')
} else if (System.getProperty("os.arch").equals("aarch64")) {
    project.ext.set('arch', 'aarch64')
}

project.ext.set('libPath', '../salmon-native/build/libs/salmon/shared')
if(project.arch == 'aarch64') {
	// for arm64 we use the gcc version since gradle does not seem to support that build
	project.ext.set('libPath', '../../salmon-libs-gcc/lib/aarch64');
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'net.java.dev.jna:jna:5.13.0'
    implementation 'net.java.dev.jna:jna-platform:5.13.0'

    implementation project(path: ':salmon-core')
    implementation project(path: ':salmon-fs')

    testImplementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    testImplementation 'net.java.dev.jna:jna:5.13.0'
    testImplementation 'net.java.dev.jna:jna-platform:5.13.0'
}

test {
    useJUnitPlatform()
    systemProperty "java.library.path", project.libPath
	afterTest { desc, result -> 
        logger.quiet "Test ${desc.className} ${desc.name} : ${result.resultType}"
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperty "java.library.path", project.libPath
}

sourceSets {
    main.java.srcDirs += '../../../src/java/salmon-win/'
    test.java.srcDirs += '../../../test/salmon-win-test-java/'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                packaging = 'jar'
                description = project.description

                url = 'https://github.com/mku11/Salmon-AES-CTR'

                scm {
                    url = 'https://github.com/mku11/Salmon-AES-CTR'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/mku11/Salmon-AES-CTR/blob/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'mku'
                        name = 'Max Kas'
                    }
                }
            }

            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven  {
            url uri("${projectDir}/../../../../output/maven/releases")
        }
        mavenLocal()
    }
}
