import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'c'
    id 'maven-publish'
}

group 'com.mku.salmon'
description = 'Salmon AES256 CTR native encryption library'
version '1.0.6-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {

}

sourceSets {
    // workaround: since we use external source directories the IDE
    // cannot see the c source so we add them here as java files
    main.java.srcDirs += '../../../src/c'
}

model {
    platforms {
        x64 {
            architecture "x86_64"
        }
    }
    components {
        // needs VC, cygwin GCC, or minGW in windows, or Linux GCC, MacOS Darwin GCC
        // the plugin will find it automatically
        salmon(NativeLibrarySpec) {
            targetPlatform "x64"
            binaries.all {
                def jvmHome = Jvm.current().javaHome
                if (targetPlatform.operatingSystem.macOsX) {
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/darwin"
                    cCompiler.args '-mmacosx-version-min=10.4'
                    linker.args '-mmacosx-version-min=10.4'
                } else if (targetPlatform.operatingSystem.linux) {
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/linux"
                    cCompiler.args '-D_FILE_OFFSET_BITS=64'
                } else if (targetPlatform.operatingSystem.windows) {
                    cCompiler.args "-I${jvmHome}/include"
                    cCompiler.args "-I${jvmHome}/include/win32"
                } else if (targetPlatform.operatingSystem.freeBSD) {
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/freebsd"
                }
                // comment out if you don't want tiny-aes
                cCompiler.args '-DUSE_TINY_AES=1'
                cCompiler.args '-DAES256=1'

				cCompiler.args '-I'+projectDir+'/../../../src/c/salmon/include'
				cCompiler.args '-I'+projectDir+'/../../../src/c/salmon-jni/include'
				
                cCompiler.args '-O2'
				cCompiler.args '-maes'
            }
            sources {
                c {
                    source {
                        srcDirs "../../../src/c"
                        include "**/salmon/*.c"
                        include "**/salmon/salmon-aes-intr/*.c"
                        include "**/salmon-jni/*.c"
                        include "**/tiny-AES-c/aes.c"
                    }
                }
            }
        }
    }
}

task assembleArtifact(type: Zip, group: 'com.mku.salmon') {
    archiveName 'salmon-native.zip'
    destinationDir file("$buildDir/libs/")
    from("build/libs/salmon/shared") {
        into 'lib'
    }
    from '../../../../LICENSE'
    from '../../../../NOTICE'
    from("../../../../c/src/salmon/") {
        into 'headers/salmon'
        include '*.h'
    }
    from("../../../src/c/salmon/salmon-aes-intr/") {
        into 'headers/salmon/salmon-aes-intr'
        include '*.h'
    }
    from("../../../src/c/salmon/salmon-jni/") {
        into 'headers/salmon/salmon-jni'
        include '*.h'
    }
    from("../../../src/c/src/tiny-AES-c") {
        into 'headers/tiny-AES-c'
        include '*.h'
        include 'unlicense.txt'
    }
    description "Assemble archive $archiveName into ${relativePath(destinationDir)}"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact source: assembleArtifact, extension: 'zip'
            pom {
                name = project.name
                packaging = 'zip'
                description = project.description
                url = 'https://github.com/mku11/Salmon-AES-CTR'

                scm {
                    url = 'https://github.com/mku11/Salmon-AES-CTR'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/mku11/Salmon-AES-CTR/blob/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'mku'
                        name = 'Max Kas'
                    }
                }
            }
        }
    }
    repositories {
        maven  {
            url uri("${projectDir}/../../../../output/maven/releases")
        }
        mavenLocal()
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'salmon.Main'
    }
}

//TODO: is this needed to run as part of gradle build tasks?
classes.dependsOn 'salmonSharedLibrary'