import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'c'
    id 'cpp'
    id 'maven-publish'
}

group 'com.mku.salmon'
description = 'Salmon AES256 CTR native encryption library'
version '3.0.2'

import org.gradle.internal.os.OperatingSystem
OperatingSystem os = OperatingSystem.current();

println "OS: ${os.familyName}"
println "ARCH: ${System.getProperty("os.arch")}"

if (os.isLinux()) {
    project.ext.set('os', 'linux')
} else if (os.isWindows()) {
    project.ext.set('os', 'win')
} else if (os.isMacOsX()) {
    project.ext.set('os', 'macos')
}

if (System.getProperty("os.arch").equals("amd64") || System.getProperty("os.arch").equals("x86_64")) {
    project.ext.set('arch', 'x86_64')
} else if (System.getProperty("os.arch").equals("aarch64")) {
    project.ext.set('arch', 'aarch64')
}

println "arch: " + project.arch 

// set to true to enable opencl or set via env vars
// make sure you have installed OpenCL in your system see README for instructions
project.ext.set('ENABLE_GPU', System.getProperty('ENABLE_GPU', "false").equals("true"))
project.ext.set('DEFAULT_OPENCL_ROOT', "${projectDir}\\..\\..\\..\\deps\\OpenCL\\OpenCL-SDK-v2025.07.23-Win-x64")

println "DEFAULT_OPENCL_ROOT: " + project.DEFAULT_OPENCL_ROOT

// windows
project.ext.set('OPENCL_INCLUDE', System.getProperty('OPENCL_INCLUDE', project.DEFAULT_OPENCL_ROOT + "\\include"))
project.ext.set('OPENCL_LIB', System.getProperty('OPENCL_LIB', project.DEFAULT_OPENCL_ROOT + "\\lib"))

// linux
// project.ext.set('OPENCL_INCLUDE', System.getProperty('OPENCL_INCLUDE',"/usr/include")
// project.ext.set('OPENCL_LIB', System.getProperty('OPENCL_LIB',"/usr/lib/x86_64-linux-gnu/")

println "Using OPENCL_INCLUDE: " + project.OPENCL_INCLUDE
println "Using OPENCL_LIB: " + project.OPENCL_LIB

repositories {
    mavenCentral()
}

dependencies {

}

sourceSets {
    // workaround: since we use external source directories the IDE
    // cannot see the c source so we add them here as java files
    main.java.srcDirs += '../../../src/c'
}

model {
    platforms {
        currentPlatform {
            architecture project.arch
        }
    }
    components {
        // needs VS, cygwin GCC, or minGW in windows, or Linux GCC, MacOS Darwin GCC
        // the plugin will find it automatically
        salmon(NativeLibrarySpec) {
            targetPlatform "currentPlatform"
            binaries.all {
                def jvmHome = Jvm.current().javaHome
                if (targetPlatform.operatingSystem.macOsX) {
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/darwin"
                    cCompiler.args '-mmacosx-version-min=10.4'
                    cCompiler.args '-std=gnu99'
                    cppCompiler.args '-std=c++11'
                    linker.args '-mmacosx-version-min=10.4'
                } else if (targetPlatform.operatingSystem.linux) {
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/linux"
                    cCompiler.args '-D_FILE_OFFSET_BITS=64'
                } else if (targetPlatform.operatingSystem.windows) {
                    cCompiler.args "-I${jvmHome}/include"
                    cCompiler.args "-I${jvmHome}/include/win32"
                } else if (targetPlatform.operatingSystem.freeBSD) {
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/freebsd"
                }
                cCompiler.args '-I' + projectDir + '/../../../src/c/salmon/include'
                cCompiler.args '-I' + projectDir + '/../../../src/c/salmon-jni/include'

                if (project.ENABLE_GPU) {
                    cCompiler.args '-DUSE_OPENCL=1'
                    cCompiler.args '-I' + project.OPENCL_INCLUDE
                    cppCompiler.args '-DCL_TARGET_OPENCL_VERSION=120'
                    if (targetPlatform.operatingSystem.windows) {
                        linker.args '/LIBPATH:' + project.OPENCL_LIB
                        linker.args 'OpenCL.lib'
                    } else if (targetPlatform.operatingSystem.linux) {
                        linker.args '-L' + project.OPENCL_LIB
                        linker.args '-lOpenCL'
                    } else if (targetPlatform.operatingSystem.macOsX){
						cCompiler.args '-framework'
						cCompiler.args 'OpenCL'
						linker.args '-framework'
						linker.args 'OpenCL'
		   			}
                }

				cCompiler.args '-g'
                cCompiler.args '-O2'
                cCompiler.args '-maes'
				
				// ___constant is needed only for OpenCL kernel compiler for macos 
				// for gcc and clang we use an empty macro to suppress the warnings
				cCompiler.args '-D__constant='
				cCompiler.args '-D__global='
            }
            sources {
                c {
                    source {
                        srcDirs "../../../src/c"
                        include "**/salmon/src/*.c"
                        include "**/salmon-jni/src/*.c"
                    }
                }
                cpp {
                    source {
                        srcDirs "../../../src/c/"
                        include "**/salmon/src/kernels/*.cpp"
                    }
                }
            }
        }
    }
}

task assembleArtifact(type: Zip, group: 'com.mku.salmon') {
    archiveName 'salmon-native.zip'
    archiveClassifier.set(project.os + '-' + project.arch)
    destinationDir file("$buildDir/libs/")
    from("build/libs/salmon/shared") {
        into 'lib'
    }
    from '../../../../LICENSE'
    from '../../../../NOTICE'

    from("../../../src/c/salmon/include/") {
        into 'salmon/include'
        include '*.h'
    }
    from("../../../src/c/salmon-jni/include/") {
        into 'salmon-jni/include'
        include '*.h'
    }
    description "Assemble archive $archiveName into ${relativePath(destinationDir)}"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact source: assembleArtifact, extension: 'jar'
            pom {
                name = project.name
                packaging = 'jar'
                description = project.description
                url = 'https://github.com/mku11/Salmon-AES-CTR'

                scm {
                    url = 'https://github.com/mku11/Salmon-AES-CTR'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/mku11/Salmon-AES-CTR/blob/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'mku'
                        name = 'Max Kas'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url uri("${projectDir}/../../../../output/maven/releases")
        }
        mavenLocal()
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'salmon.Main'
    }
}