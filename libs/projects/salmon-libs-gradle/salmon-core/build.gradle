plugins {
    id 'java-library'
    id "me.champeau.jmh" version "0.7.1"
    id 'maven-publish'
}

group 'com.mku.salmon'
description = 'Salmon AES256 CTR core encryption library'
version '2.2.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
    systemProperty "java.library.path", "../salmon-native/build/libs/salmon/shared"
	systemProperty "ENABLE_GPU", System.getProperty('ENABLE_GPU')
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperty "java.library.path", "../salmon-native/build/libs/salmon/shared"
}

sourceSets {
    main.java.srcDirs += '../../../src/java/salmon-core/'
    test.java.srcDirs += '../../../test/salmon-core-test-java/'
    test.java.srcDirs += '../../../test/salmon-native-test-java/'
}

jmh {
    warmupIterations = 2
    iterations = 1
    jmhTimeout = '5s'
    fork = 1
    jvmArgs = ['-Djava.library.path=../salmon-native/build/libs/salmon/shared']
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                packaging = 'jar'
                description = project.description
                url = 'https://github.com/mku11/Salmon-AES-CTR'

                scm {
                    url = 'https://github.com/mku11/Salmon-AES-CTR'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/mku11/Salmon-AES-CTR/blob/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'mku'
                        name = 'Max Kas'
                    }
                }
            }

            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven  {
            url uri("${projectDir}/../../../../output/maven/releases")
        }
        mavenLocal()
    }
}
