plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'com.mku.salmon'
description = 'Salmon AES256 CTR filesystem encryption library'
version '2.2.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation project(':salmon-core').sourceSets.test.output

    implementation project(path: ':salmon-core')
    implementation("org.apache.httpcomponents:httpclient:4.5.13")
    implementation("org.apache.httpcomponents:httpmime:4.5.13")
    implementation 'org.json:json:20240303'


    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
}

test {
    useJUnitPlatform()
	systemProperty 'testDir', System.getProperty('testDir')
}

tasks.withType(Test) {
    systemProperty "java.library.path", "../salmon-native/build/libs/salmon/shared"
}

sourceSets {
    main.java.srcDirs += '../../../src/java/salmon-fs/'
    test.java.srcDirs += '../../../test/salmon-fs-test-java/'
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                packaging = 'jar'
                description = project.description
                url = 'https://github.com/mku11/Salmon-AES-CTR'

                scm {
                    url = 'https://github.com/mku11/Salmon-AES-CTR'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/mku11/Salmon-AES-CTR/blob/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'mku'
                        name = 'Max Kas'
                    }
                }
            }
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven  {
            url uri("${projectDir}/../../../../output/maven/releases")
        }
        mavenLocal()
    }
}